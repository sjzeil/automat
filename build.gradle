plugins {
	id 'base'
	id 'org.hidetake.ssh' version '2.10.1'
}

task copyDocs (type: Copy, dependsOn: ['docs:bake']) {
	from 'docs/build/jbake'
	into 'build/temp/help'
}

task copyDist (type: Copy, dependsOn: ['automatWeb:build', 'grader:build']) {
	from 'automatWeb/build/dist'
	from 'grader/build/dist'
	into 'build/temp'
}



task zipDist (type: Zip, dependsOn: ['copyDist', 'copyDocs']) {
	from 'build/temp'
}

assemble.dependsOn 'zipDist'


task assembleTest (type: Copy, dependsOn: ['copyDist', 'copyDocs']) {
	from 'build/temp'
	from 'automatWeb/test/resources'
	into './build/test'

    // Create web pages for local testing without CGI server
	doLast {
		String testDirectory = project.projectDir.getAbsolutePath() + '/build/test'
		new ProcessBuilder('perl',  'automat.cgi',  'action=editor',  'problem=testProblem', 'test=instr1') .
		    directory(new File(testDirectory)) .
		    redirectOutput(new File(testDirectory + '/editTestProblem.html')) .
		    start()
		
		new ProcessBuilder('perl',  'automat.cgi',  'action=editor',  'problem=selfAssessProblem', 'test=student1') .
		    directory(new File(testDirectory)) .
		    redirectOutput(new File(testDirectory  + '/editSAProblem.html')) .
		    start()
		
	}
}

task zipTest (type: Zip, dependsOn: ['assembleTest']) {
	from 'build/test'
	archiveName 'automatTest.zip'
}



remotes {
  webServer {
    host = 'linux.cs.odu.edu'
    user = 'zeil'
	agent = true
	//identity = '/home/zeil/.ssh/homepc'
  }
}

task deploy (dependsOn: 'assemble') {
  doLast {
    ssh.run {
      session(remotes.webServer) {
       put from: "$buildDir/distributions/automat.zip", into: '/home/zeil/secure_html/automat/'
       execute 'unzip -u -o /home/zeil/secure_html/automat/automat.zip -d /home/zeil/secure_html/automat' 
       execute 'chmod +x /home/zeil/secure_html/automat/*.cgi'
      }
    }
  }
}

task deployTest (dependsOn: 'zipTest') {
  doLast {
    ssh.run {
      session(remotes.webServer) {
       put from: "$buildDir/distributions/automat.zip", into: '/home/zeil/secure_html/automatTest/'
       execute 'unzip -u -o /home/zeil/secure_html/automatTest/automatTest.zip'
       execute 'chmod +x /home/zeil/secure_html/automatTest/*.cgi'
      }
    }
  }
}

