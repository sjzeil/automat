plugins {
	id 'base'
	id 'org.hidetake.ssh' version '2.10.1'
}


/** 
 * Change the webserver info and the uploadLocation
 * to your own desired deploment location.
 */

remotes {
  webServer {
    host = 'linux.cs.odu.edu'
    user = 'zeil'
	agent = true
  }
}

ext {
   uploadLocation = '/home/zeil/secure_html/automat'
}




task copyDocs (type: Copy, dependsOn: ['docs:bake']) {
	from 'docs/build/jbake'
	into 'build/temp/help'
}

task copyDist (type: Copy, dependsOn: ['automatWeb:build', 'grader:build']) {
	from 'automatWeb/build/dist'
	from 'grader/build/dist'
	into 'build/temp'
}



task zipDist (type: Zip, dependsOn: ['copyDist', 'copyDocs']) {
	from 'build/temp'
}

assemble.dependsOn 'zipDist'


task assembleTest (type: Copy, dependsOn: ['copyDist', 'copyDocs']) {
	from 'build/temp'
	from 'automatWeb/test/resources'
	into './build/test'

    // Create web pages for local testing without CGI server
	doLast {
		String testDirectory = project.projectDir.getAbsolutePath() + '/build/test'
		new ProcessBuilder('perl',  'automat.cgi',  'action=editor',  'problem=testProblem', 'test=instr1') .
		    directory(new File(testDirectory)) .
		    redirectOutput(new File(testDirectory + '/editTestProblem.html')) .
		    start()
		
		new ProcessBuilder('perl',  'automat.cgi',  'action=editor',  'problem=selfAssessProblem', 'test=student1') .
		    directory(new File(testDirectory)) .
		    redirectOutput(new File(testDirectory  + '/editSAProblem.html')) .
		    start()
		
	}
}

task zipTest (type: Zip, dependsOn: ['assembleTest']) {
	from 'build/test'
	archiveName 'automatTest.zip'
}





task deploy (dependsOn: 'assemble') {
  doLast {
    ssh.run {
      session(remotes.webServer) {
       put from: "$buildDir/distributions/automat.zip", into: uploadLocation
       execute "unzip -u -o ${uploadLocation}/automat.zip -d ${uploadLocation}"
       execute "find ${uploadLocation}/ -type d | xargs chmod 770"
       execute "find ${uploadLocation}/ -type f | xargs chmod 660"
       execute "find ${uploadLocation}/ -name '*.cgi' | xargs chmod 771"
      }
    }
  }
}


